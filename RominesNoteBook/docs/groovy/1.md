# Groovy 和 Java 的主要区别  

## 默认引用
默认引用的包，不需要`import`语句  
```


    java.io.*

    java.lang.*

    java.math.BigDecimal

    java.math.BigInteger

    java.net.*

    java.util.*

    groovy.lang.*

    groovy.util.*


```

## 重载方法
groovy 的重载方法在运行时被选择调用，而 Java 的重载方法是在编译时根据声明类型进行选择。  
```
    public static void test(String t1){
        System.out.println(t1 + " at test1");
    }

    public static void test(Object t2){
        System.out.println(t2 + " at test2");
    }

    public static void main(String[] args) {
        Object t = "123";
        test(t);
    }
```
Java 的结果是 `123 at test2`  
Groovy 的结果是 `123 at test1`  

## 数组语法
groovy 中的 `{...}` 语句为闭包保留，不能用作声明数组。groovy 声明数组的方式如下  
```
int arr = [1,2,3];
// groovy 3.0+ 
def arr = new int[]{1,2,3};
```

## 包私有字段 
省略类成员的修饰符不会像 Java 那样成为一个包私有字段。  
```
class Person {
    String name
}
```
这样声明时创建一个属性，即一个私有字段以及一个`getter`和一个`setter`  
声明包私有字段需要使用`@PackageScope`  
```
Class Persion{
    @PackageScope String name
}
```

## ARM 块
Java 的 ARM 块
```
Path file = Paths.get("/path/to/file");
Charset charset = Charset.forName("UTF-8");
try (BufferedReader reader = Files.newBufferedReader(file, charset)) {
    String line;
    while ((line = reader.readLine()) != null) {
        System.out.println(line);
    }

} catch (IOException e) {
    e.printStackTrace();
}
```
Groovy 3+ 也支持同样的块。但Groovy提供了各种依赖闭包的方法  
```
new File('/path/to/file').eachLine('UTF-8') {
   println it
}
//或
new File('/path/to/file').withReader('UTF-8') { reader ->
   reader.eachLine {
       println it
   }
}
```

## 内部类  
匿名内部类和嵌套类和Java基本相同，但仍然有些区别：  
例如，在这些内部类里访问的本地变量不需要是 final。Groovy在生成内部类字节码时利用了一些闭包`groovy.lang.Closure`的细节  
### 静态内部类  
```
class A {
    static class B {}
}

new A.B()
```
最受支持的内部类。如果你需要一个内部类，那么最好声明成静态内部类  
### Anonymous Inner Classes 匿名内部类  
```
import java.util.concurrent.CountDownLatch
import java.util.concurrent.TimeUnit

CountDownLatch called = new CountDownLatch(1)

Timer timer = new Timer()
timer.schedule(new TimerTask() {
    void run() {
        called.countDown()
    }
}, 0)

assert called.await(10, TimeUnit.SECONDS)
```
### 非静态内部类的实例创建
Java  
```
public class Y {

    public class X{}

    public X foo(){
        return new X();
    }

    public static X createX(Y y){
        return y.new X();
    }
}
```
Groovy
```
class Y {
    class X{
        void run(){
            println "ok";
        }

        void Test(){
            TestY()
        }
    }

    X foo(){
        return new X()
    }

    void TestY(){
        println "ok"
    }

    static X createX(Y y){
        return new X(y);
    }
}
```
Groovy 3.0.0以后也支持`y.new X()` 的写法了  
Groovy 支持不带任何参数直接调用一个需要一个形参的方法，此时这个形参值为`null`。这个规则同样适用于构造方法。如果这里用`Y.createX()`创建`X`实例，再调用`Test`方法则会发生空指针异常  

## Lambda 表达式和方法引用操作符  
Java
```
Runnable run = () -> System.out.println("Run");  // Java
list.forEach(System.out::println);
```
Groovy支持同样的语法。但早期的 Groovy 应该使用闭包
```
Runnable run = { println 'run' }
list.each { println it } // or list.each(this.&println)
```

## GStrings  
单引号字符串字面量被解释为`String`，而双引号字符串字面量被解释为`GString`值。  
使用Java和Groovy编译器编译一个包含`$`字符的字符串字面量类，有可能会因为编译错误而导致失败，或者生成略有不同的代码。  

补充：  
Groovy中带小数点的数字字面量被解释为`BigDecimal`，避免了类似`0.7+0.1=0.7999999999...`的问题  

## 字符和字符串字面量  
单引号字符串字面量为`String`，双引号字符串字面量为`String`或`GString`，取决于是否包含插值表达式  
```
assert 'c'.getClass() == String
assert "C".getClass() == String
assert "c${1}".getClass() in GString
```
只有在给一个`char`类型变量赋值时，Groovy会将一个单字符字符串转换为`char`。  
Groovy支持两种类型转换语法。在转换成`char`时Groovy style可以使用第一个字符，而C style会报错  
```
char c = "123" as char
c = "123".asType(char as Class<Object>) 
c = (char)"123" //会报错
```

## 基本类型和包装类 Primitives and wrappers  
因为Groovy的所有内容都使用对象，所以会自动使用包装类。因此和Java相反，Groovy的装箱优先于扩宽。
```
class Test5 {

    static void f1(long i){
        println 'this is f1'  //Java 会走这个方法
    }

    static void f1(Integer i){
        println 'this is f2'  //Groovy 会走这个方法  
    }

    static void main(String[] args) {
        int a = 1
        f1(a)
    }
}
```

## == 表达式  
Java中的`==`表示基本类型或者对象相同。而在Groovy中意味着都相同，相当于`a.compareTo(b) == 0`，或者计算`Comparable`对象时的`a.equals(b)`。  
计算是否为同一对象的引用，使用`is`方法：`a.is(b)`。Groovy 3之后还可以使用`===`： `a === b` `a !== b`  

需要自定义是否相等，还是通过定义新的`equals`方法  

## 转换 Conversions  
Java会自动进行拓宽或收窄的转换（如int和byte的转换）  

| Converts from | boolean | byte | short | char | int | long | float | double | 
| :- | :-: | :-: | :-: | :-: | :-: | :-: | :-: | :-: |
| boolean | - | N | N | N | N | N | N | N | 
| byte | N | - | Y | C | Y | Y | Y | Y |
| short | N | C | - | C | Y | Y | Y | Y |
| char | N | C | C | - | Y | Y | Y | Y |
| int | N | C | C | C | - | Y | T | Y |
| long | N | C | C | C | C | - | T | T |
| float | N | C | C | C | C | C | - | Y |
|double | N | C | C | C | C | C | C | - |

- `Y`表示可以转换  
- `C`可以显式类型转换  
- `T`可以转换但数据会被截断  
- `N`不可以转换  

Groovy 的转换：  

| Converts from | boolean | Boolean | byte | Byte | short | Short | char | Character | int | Integer | long | Long | BigInteger | float | Float | double | Double | BigDecimal | 
| :- | :-: | :-: | :-: | :-: | :-: | :-: | :-: | :-: | :-: | :-: | :-: | :-: | :-: | :-: | :-: | :-: | :-: | :-: |
| boolean | - | B | N | N | N | N | N | N | N | N | N | N | N | N | N | N | N | N |
| Boolean | B | - | N | N | N | N | N | N | N | N | N | N | N | N | N | N | N | N |
| byte | T | T | - | B | Y | Y | Y | D | Y | Y | Y | Y | Y | Y | Y | Y | Y | Y |
| Byte | T | T | B | - | Y | Y | Y | D | Y | Y | Y | Y | Y | Y | Y | Y | Y | Y |
| short | T | T | D | D | - | B | Y | D | Y | Y | Y | Y | Y | Y | Y | Y | Y | Y |
| Short | T | T | D | T | B | - | Y | D | Y | Y | Y | Y | Y | Y | Y | Y | Y | Y |
| char | T | T | Y | D | Y | D | - | D | Y | D | Y | D | D | Y | D | Y | D | D |
| Character | T | T | D | D | D | D | D | - | D | D | D | D | D | D | D | D | D | D |
| int | T | T | D | D | D | D | Y | D | - | B | Y | Y | Y | Y | Y | Y | Y | Y |
| Integer | T | T | D | D | D | D | Y | D | B | - | Y | Y | Y | Y | Y | Y | Y | Y |
| long | T | T | D | D | D | D | Y | D | D | D | - | B | Y | T | T | T | T | Y |
| Long | T | T | D | D | D | T | Y | D | D | T | B | - | Y | T | T | T | T | Y |
| BigInteger | T | T | D | D | D | D | D | D | D | D | D | D | - | D | D | D | D | T |
| float | T | T | D | D | D | D | T | D | D | D | D | D | D | - | B | Y | Y | Y |
| Float | T | T | D | T | D | T | T | D | D | T | D | T | D | B | - | Y | Y | Y |
| double | T | T | D | D | D | D | T | D | D | D | D | D | D | D | D | - | B | Y |
| Double | T | T | D | T | D | T | T | D | D | T | D | T | D | D | T | B | - | Y |
| BigDecimal | T | T | D | D | D | D | D | D | D | D | D | D | D | T | D | T | D | - |


- `Y`表示可以转换  
- `D`动态编译或者显示类型转换，compiled dynamically or explicitly cast  
- `T`可以转换但是会被截断，truncated  
- `B`装箱或者拆箱的操作，Boxing  
- `N`不能进行转换  

## 额外的关键字  
- `var` （Groovy 3 ）
- `def`  
- `as`  
- `in`  
- `trait`  
- `it` //within closures  
Groovy 允许关键字出现在 Java 中不合法的地方，如 `var var = [def: 1, as: 2,in: 3,trait: 4]`，但不建议这样使用。  