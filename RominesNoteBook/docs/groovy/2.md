# The Groovy Development Kit

## Working with IO 输入输出
Groovy 提供了很多 [help methods](https://groovy-lang.org/gdk.html)。  使用 Java 代码处理文件、流、reader...的时候，就可以使用这些方便的方法

特别关注：  

- [`java.io.File`](http://docs.groovy-lang.org/latest/html/groovy-jdk/java/io/File.html)
- [`java.io.InputStream`](http://docs.groovy-lang.org/latest/html/groovy-jdk/java/io/InputStream.html)
- [`java.io.OutputStream`](http://docs.groovy-lang.org/latest/html/groovy-jdk/java/io/OutputStream.html)
- [`java.io.Reader`](http://docs.groovy-lang.org/latest/html/groovy-jdk/java/io/Reader.html)
- [`java.io.Writer`](http://docs.groovy-lang.org/latest/html/groovy-jdk/java/io/Writer.html)
- [`java.nio.file.Path`](http://docs.groovy-lang.org/latest/html/groovy-jdk/java/nio/file/Path.html)

`help methods`的重点部分示例：  

### Reading files

输出一个text文件的每一行

```
new File(baseDir, 'haiku.txt').eachLine{ line -> 
    println line
}
```

`eachLine`是Groovy给`File`类添加的`help method`。这个方法有很多的变体（variants），例如如果需要输出行号代码如下：  

```
new File(baseDir, 'haiku.txt').eachLine{ line, nb -> 
    println "Line $nb: $line"
}
```

当`eachLine`方法块里发生任何异常时，方法会保证输入源会被完全关闭。（Groovy添加的所有IO源方法都会遵循这个原则）  

例如：当你需要使用`Reader`时，Groovy自动源管理仍然会生效。下例中的`Reader`即使运行过程中发生异常也会被 close  

```
def count = 0, MAXSIZE = 3
new File(baseDir, 'haiku.txt').withReader{ reader -> 
    while (reader.readLine()){
        if( ++count > MAXSIZE ){
            throw new RuntimeException('Haiku should only have 3 verses')
        }
    }
}
```

---

将文件的每一行放到一个`List`里  

```
def list = new File(baseDir, 'haiku.txt').collect{ it }
```

---

使用 `as` 将文件的每一行放到一个数组里  

```
def array = new File(baseDir, 'haiku.txt') as String[]
```

---

将文件数据放入一个 `byte[]`

```
byte[] contents = file.bytes  
```

---

获取 `File` 对象的 `InputStream`  

```
def is = new File(baseDir, 'haiku.txt').newInputStream()  
//do something ...
is.close()
```

省略手动 `close` 的写法  
```
new File(baseDir, 'haiku.txt').withInputStream{ inputStream ->
    //do something ...
}
```

### Writing Files  
```
new File(baseDir,'haiku.txt').withWriter('utf-8'){ writer ->
    writer.writeLine 'Into the ancient pond'
    writer.writeLine 'A frog jumps'
    writer.writeLine 'Water's sound!'
}
```

---

使用 `<<` 操作符  

```
new File(baseDir,'haiku.txt') << '''Into the ancient pond
A frog jumps
Water's sound!'''
```

---

不使用文本，直接使用 bytes 

```
file.bytes = [66,22,11]
```

---

使用输出流

```
def os = new File(baseDir, 'data.bin').newOutputStream()
// do something ...
os.close()
```

省略 close 的写法  

```
new File(baseDir, 'data.bin').withOutputStream(){ stream -> 
    // do something ...
}
```

### Traversing file trees 遍历文件树  

遍历文件夹下的所有文件  

```
dir.eachFile { file -> 
    println file.name
}
// 遍历匹配的文件
dir.eachFileMatch( ~/.*\.txt/ ){ file -> 
    println file.name
}
```

--- 

遍历并递归查询子文件夹  

```
dir.eachFileRecurse { file ->
    println file.name
}
//只遍历文件夹
dir.eachFileRecurse( FileType.DIRECTORIES ){ file ->
    println file.name
}
```

--- 

自定义遍历规则  

```
dir.traverse { file -> 
    if (file.directory && file.name == 'bin'){
        FileVisitResult.TERMINATE //终止遍历
    }else{
        println file.name
        FileVisitResult.CONTINUE //继续遍历
    }
}
```

关于 `FileVisitResult`

- CONTINUE 继续
- SKIP_SIBLINGS 跳过同级
- SKIP_SUBTREE 跳过同级的子级
- TERMINATE 终止

关于 `traverse` 方法  

```
    public static void traverse(final File self,
            @NamedParam(value = "type", type = FileType.class)
            @NamedParam(value = "preDir", type = Closure.class)
            @NamedParam(value = "preRoot", type = Boolean.class)
            @NamedParam(value = "postDir", type = Closure.class)
            @NamedParam(value = "postRoot", type = Boolean.class)
            @NamedParam(value = "visitRoot", type = Boolean.class)
            @NamedParam(value = "maxDepth", type = Integer.class)
            @NamedParam(value = "filter", type = Object.class)
            @NamedParam(value = "nameFilter", type = Object.class)
            @NamedParam(value = "excludeFilter", type = Object.class)
            @NamedParam(value = "excludeNameFilter", type = Object.class)
            @NamedParam(value = "sort", type = Closure.class)
            final Map<String, ?> options)
                throws FileNotFoundException, IllegalArgumentException {
        final Closure visit = (Closure) options.remove("visit");
        traverse(self, options, visit);
    }
```

示例 

```
dir.traverse (
    preDir :{
        if(it.name == "out"){
            FileVisitResult.SKIP_SUBTREE
        }else{
            FileVisitResult.CONTINUE
        }
    },
    file -> {
        println file.name
    }
)
```

对比 Java 的文件遍历  

```
public static void main(String[] args) throws IOException {
    Files.walkFileTree(Paths.get("C:\\Users\\Romine\\Desktop\\svn\\project\\groovyTest"),new SimpleFileVisitor<Path>(){
        @Override
        public FileVisitResult preVisitDirectory(Path dir, BasicFileAttributes attrs) throws IOException {
            System.out.println(dir.toFile().getName());
            return super.preVisitDirectory(dir, attrs);
        }
        @Override
        public FileVisitResult postVisitDirectory(Path dir, IOException exc) throws IOException {
            System.out.println(dir.toFile().getName());
            if(dir.toFile().getName() == "out"){
                return FileVisitResult.SKIP_SUBTREE;
            }else {
                return super.postVisitDirectory(dir, exc);
            }
        }
        @Override
        public FileVisitResult visitFile(Path file, BasicFileAttributes attrs) throws IOException {
            System.out.println(file.toFile().getName());
            return super.visitFile(file, attrs);
        }
        @Override
        public FileVisitResult visitFileFailed(Path file, IOException exc) throws IOException {
            return super.visitFileFailed(file, exc);
        }
    });
}
```

### Data and objects

数据和对象的序列化  

```
boolean b = true
String message = 'Hello from Groovy'
// Serialize data into a file
file.withDataOutputStream{ out -> 
    out.writeBoolean(b)
    out.writeUTF(message)
}
// ...
/// Then read it back
file.withDataInputStream{ input -> 
    assert input.readBoolean() == b
    assert input.readUTF() == message
}
```

对象序列化，需要实现`Serializable`接口。  

### 执行外部过程 Executing External Processes

这里的外部过程，应该是系统终端  
基本用法：String 格式的命令行，执行`execute()`方法  

```
def process = "ls -l".execute() // 执行 ls 命令
println "Found text ${process.text}" // 打印 ls 执行后的输出
```

